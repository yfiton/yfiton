/*
 * Copyright 2015 Laurent Pellegrino
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'application'
    id 'net.researchgate.release' version '2.6.0'
}

allprojects {
    apply plugin: 'java'

    group = 'com.yfiton'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    defaultTasks 'clean', 'installDist'

    dependencies {
        compile 'ch.qos.logback:logback-classic:1.1.3'
        compile 'com.google.guava:guava:19.0'
        compile 'commons-configuration:commons-configuration:1.10'
    }

    repositories {
        jcenter()
        maven {
            url "https://jitpack.io"
        }
    }
}

def exportedProjects = [
        ":yfiton-api",
        ":yfiton-core",
        ":yfiton-oauth"
]

task alljavadoc(type: Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
}

subprojects {
    apply plugin: 'java'

    dependencies {
        testCompile 'com.google.truth:truth:0.27'
        testCompile 'junit:junit:4.12'
    }
}

mainClassName = 'com.yfiton.cli.YfitonCli'

dependencies {
    compile(
            project(':yfiton-cli')
    )
}
